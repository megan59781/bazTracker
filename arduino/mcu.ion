#include <Arduino.h>
#if defined(ESP8266)
  #include <ESP8266WiFi.h>
#endif
#include <Firebase_ESP_Client.h>

#include <SoftwareSerial.h>
#include <ArduinoJson.h>

// Provide the token generation process info.
#include "addons/TokenHelper.h"
//Provide the RTDB payload printing info and other helper functions.
#include "addons/RTDBHelper.h"

// Insert your network credentials
#define WIFI_SSID "5G COVID mast"
#define WIFI_PASSWORD "earht307"

// Insert Firebase project API Key
#define API_KEY "AIzaSyAbqWFBG9yTQPWTazHVNXU32C3F3XKgXgk"

// Insert RTDB URLefine the RTDB URL */
#define DATABASE_URL "https://baztracker-default-rtdb.europe-west1.firebasedatabase.app"

// D6 = Rx & D5 = Tx
SoftwareSerial nodemcu(D6, D5);

// Define Firebase Data object
FirebaseData fbdo;

FirebaseAuth auth;
FirebaseConfig config;

// Varibles for db load time
unsigned long sendDataPrevMillis = 0;
int count = 0;
bool signupOK = false;

void setup(){
  Serial.begin(9600);
  nodemcu.begin(9600);

  WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
  Serial.print("Connecting to Wi-Fi");
  while (WiFi.status() != WL_CONNECTED){
    Serial.print(".");
    delay(300);
  }
  Serial.println();
  Serial.print("Connected with IP: ");
  Serial.println(WiFi.localIP());
  Serial.println();

  config.api_key = API_KEY;
  config.database_url = DATABASE_URL;

  // Sign up function to connect to firebase
  if (Firebase.signUp(&config, &auth, "", "")){
    Serial.println("ok");
    signupOK = true;
  }
  else{
    Serial.printf("%s\n", config.signer.signupError.message.c_str());
  }

  // Assign the callback function for the long running token generation task
  config.token_status_callback = tokenStatusCallback;
  
  Firebase.begin(&config, &auth);
  Firebase.reconnectWiFi(true);
}

void loop(){
  if (WiFi.status() != WL_CONNECTED) {
    // Display reconnect to Wi-Fi if not connected
    Serial.println("Connecting to Wi-Fi...");
    WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
    while (WiFi.status() != WL_CONNECTED) {
      Serial.print(".");
      delay(500);
    }
    Serial.println("\nConnected to Wi-Fi with IP: " + WiFi.localIP().toString());
  }

  // Set Json Object
  StaticJsonBuffer<1000> jsonBuffer;
  JsonObject& data = jsonBuffer.parseObject(nodemcu);

  //StaticJsonBuffer<1000> jsonBuffer;
  JsonObject& dataSend = jsonBuffer.createObject();

  if (data == JsonObject::invalid()) {
    //Serial.println("Invalid Json Object");
    jsonBuffer.clear();
    return;
  }

  // Water level and time and dates from Ard
  int water = data["current_water_val"];
  String wTime = data["water_time"];
  String wDate = data["water_date"];

  // Motion and which dog detceted from Ard
  String dog = data["dog_present"];
  String dogTime = data["dog_time"];
  String dogDate = data["dog_date"];

  // Add Barry record to firebase
  if (dog == "Barry") {
      if (Firebase.RTDB.setString(&fbdo, "Dog Track/dog_present", dog)) {
        Serial.println("Success");
      } else {
        Serial.println("FAILED");
        Serial.println("REASON: " + fbdo.errorReason());
      }
      if (Firebase.RTDB.setString(&fbdo, "Dog Track/dog_date", dogDate)) {
        Serial.println("Success");
      } else {
        Serial.println("FAILED");
        Serial.println("REASON: " + fbdo.errorReason());
      }
      if (Firebase.RTDB.setString(&fbdo, "Dog Track/dog_time", dogTime)) {
        Serial.println("Success");
      } else {
        Serial.println("FAILED");
        Serial.println("REASON: " + fbdo.errorReason());
      }
  }

  // Every minuite add water, time and date to Firebase
  if (Firebase.ready() && signupOK && (millis() - sendDataPrevMillis > 60000 || sendDataPrevMillis == 0)){
    sendDataPrevMillis = millis();
    // Write an Int number on the database path test/int
    if (Firebase.RTDB.setInt(&fbdo, "Water Sensor/current_val", water)){
      Serial.println("Success");
    } else {
      Serial.println("FAILED");
      Serial.println("REASON: " + fbdo.errorReason());
    }
    if (Firebase.RTDB.setString(&fbdo, "Water Sensor/water_date", wDate)) {
      Serial.println("Success");
    } else {
      Serial.println("FAILED");
      Serial.println("REASON: " + fbdo.errorReason());
    }
    if (Firebase.RTDB.setString(&fbdo, "Water Sensor/water_time", wTime)) {
      Serial.println("Success");
    } else {
      Serial.println("FAILED");
      Serial.println("REASON: " + fbdo.errorReason());
    }
  }

  // check motor updates in firebase
  if (Firebase.ready() && signupOK) {
    // Check for motor set to 1 in Firebase
    if (Firebase.RTDB.getInt(&fbdo, "Treat/button_on") && fbdo.intData() == 1) {
      dataSend["motor_on"] = 1;
      dataSend.printTo(nodemcu);
      jsonBuffer.clear();
      nodemcu.println("Motor ON");  // Send message to Arduino

      if (Firebase.RTDB.setInt(&fbdo, "Treat/button_on", 0)) {
        Serial.println("Motor status updated to 0 in Firebase");
      } else {
        Serial.println("Failed to update motor status in Firebase");
        Serial.println("REASON: " + fbdo.errorReason());
      }
    }
  }

  delay(100);
}